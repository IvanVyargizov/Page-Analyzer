plugins {
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id 'io.ebean' version '13.0.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'com.adarshr.test-logger' version '3.2.0'
}

group = 'hexlet.code'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

compileJava {
    options.release = 17
}

application {
    mainClass = 'hexlet.code.App'
}

dependencies {
    implementation 'io.javalin:javalin:4.4.0'
    implementation 'org.slf4j:slf4j-simple:1.7.36'

    implementation 'org.thymeleaf:thymeleaf:3.1.0.M1'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.1.0'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE'
    implementation 'org.webjars:bootstrap:5.1.3'

    implementation 'com.h2database:h2:2.1.210'
    implementation 'org.postgresql:postgresql:42.3.3'

    implementation 'io.ebean:ebean:13.0.0'
    implementation 'io.ebean:ebean-ddl-generator:13.0.0'
    implementation 'io.ebean:ebean-querybean:13.0.0'
    implementation 'io.ebean:ebean-migration:13.0.0'
    implementation 'io.ebean:ebean-annotation:7.8'
    annotationProcessor 'io.ebean:querybean-generator:13.0.0'

    // NOTE: ebean-ddl-generator требует старые зависимости, которых нет в java 9 и выше
    implementation 'org.glassfish.jaxb:jaxb-runtime:3.0.2'
    implementation 'javax.activation:activation:1.1.1'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'io.ebean:ebean-test:13.0.0'
    testImplementation 'com.konghq:unirest-java:4.0.0-RC2'
}

test {
    useJUnitPlatform()
}

testlogger {
    showStandardStreams true
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

task generateMigrations(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hexlet.code.MigrationGenerator'
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean
